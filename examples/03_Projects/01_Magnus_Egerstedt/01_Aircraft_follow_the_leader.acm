/* 
   Works with semantic 2014
   Works with semantic 2013
   Works with semantic 2012
*/

// Magnus Egerstedt aircraft collision avoidance
// problem

class aircraft (w,c,s)
private
 phi := 0; phi' :=0;
 p := [0,0];   p' := [0,0];
 x :=0 ; y:=0;
 _3D := [];
end
 _3D := ["Box",[x,y,0],[0.1,0.1,0.1],c,[0,0,0]];
 x = dot (p, [1,0]);
 y = dot (p, [0,1]);
 p'   = s*[(cos(phi)), (sin(phi))];
 phi' = w;
end

class controller ()
private 
 i1 := [0,0]; i2 := 0;
 o  := 0;
 g  := 0;
end
 g = atan2(dot(i1,[0,1]), dot(i1,[1,0])) - i2;
 o = 10*atan2(sin(g),cos(g));
end

class Main(simulator)
private
 a := create aircraft (1,[1,0,0],1); // lead
 b := create aircraft (0,[0,1,0],2); // follower
 c := create aircraft (0,[0,0,1],2); // follower
 c1 := create controller ();
 c2 := create controller ();
end
 c1.i1 = (a.p-b.p); c1.i2 = b.phi;
 b.w   = c1.o;

 c2.i1 = (b.p-c.p); c2.i2 = c.phi;
 c.w   = c2.o; 

end