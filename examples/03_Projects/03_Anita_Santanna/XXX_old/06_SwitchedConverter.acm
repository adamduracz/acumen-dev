/* 
   Has error with semantic 2014
   Works with semantic 2013
   Works with semantic 2012
*/

// Switched converter pole
//
// Author:  Anita Sant'Anna, 2012
//
// Run this example, view Plot

// This example models a switched converter pole which can
// deliver variable voltage (power) to a circuit. 
// The voltage delivered by the converter depends on the 
// source voltage, the trigger signal and the control voltage
// as shown below:
// 
//   if Vtri >= Vca 
//     Van = Vd 
//   else 
//     Van = 0
//   end
//   
//   where:
//     Vtri: trigger signal (saw-tooth wave)  
//     Vca:  control voltage (constant)
//     Van:  voltage delivered to the circuit by the converter
//     Vd:   source voltage, can be AC or DC
//
// trigger signal, saw-tooth wave between zero and 10, frequency 1KHz


#semantics "2013 Reference"

class TriggerSource()
   private Vtri:=0; Vtri':=1000; end  
   Vtri'=1000; 
   if Vtri>=10  Vtri=0; end  
end

class SwitchedConverter(Vd,Vca)
   private 
     Van := 0; 
     trigger := create TriggerSource(); 
   end     
   if trigger.Vtri>=Vca  Van = Vd; 
     else  Van = 0; end
end

// differential equation describing the circuit being powered by the converter
class EquivalentCircuit(R,L,E)
   private i:=0; i':=0; 
      converter := create SwitchedConverter(100,8);
   end
   i' = converter.Van/L - E/L - (R/L)*i; 
end

class Main(simulator)
   private circuit := create EquivalentCircuit(0.5,0.012,40); end
   // simulator parameters
   simulator.timeStep = 0.0001;        
   simulator.endTime = 0.1;  
end

