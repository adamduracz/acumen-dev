
// Two Tanks Example
//
// Authors:  Michal Konecny and Jan Duracz
// Note:     Run using Semantics -> Enclosure

class Main(simulator) 
  private 
    x1 := 1; x1' := 2;  
    x2 := 1; x2' := -3;
    mode := "Fill1" 
  end
  switch mode
    case "Fill1" claim x1 >= 0 && x2 >= 0 
      if x2 == 0 mode := "Fill2" end; 
      x1' = 2; 
      x2' = -3
    case "Fill2" claim x1 >= 0 && x2 >= 0 
      if x1 == 0 mode := "Fill1" end; 
      x1' = -2; 
      x2' = 1
  end;
  simulator.startTime := 0;
  simulator.endTime := 2.5;
  simulator.minSolverStep := 2.5;
  simulator.minLocalizationStep := 0.001;
  simulator.minComputationImprovement := 0   
end

// Note:  For this system, invariants do not suffice 
//        to study the convergent post-Zeno dynamtics.