
// Furnace
// Authors: Adam Duracz and Jan Duracz
// A model of a controller, where events correspond to 
// switching a furnace on and off.
//
// Note on Acumen: In order to improve the enclosure for 
//  a model with many mode switches, the simulator
//  parameter minLocalizationStep has been decreased.

class Main (simulator)
  private 
    mode := "on"; x := 10; x' := 0; 
  end
  switch mode
    case "on" require x <= 25
      if x == 25
        mode := "off"
      end;
      x' = 100 - x;
    case "off" require x >= 19
      if x == 19
        mode := "on"
      end;
      x' = - x;
  end;
  simulator.endTime := 2;
  simulator.minSolverStep := 0.01;
  simulator.minLocalizationStep := 0.001; // Decreased!
  simulator.minComputationImprovement := 0;  
end