/* Two Tanks overconstrained with a helper variable
 * Authors: Michal Konecny and Jan Duracz
 * An additional invariant, based on a helper variable, stabilizes the post-Zeno enclosure.
 */
class Main(simulator) 
  private 
    mode := "Fill1"; 
    x1 := 1; x1' := 2;  
    x2 := 1; x2' := -3;
    x12 := 2; x12' := -1;
  end
  switch mode
    case "Fill1" require x1 >= 0 && x2 >= 0 && x12 >= 0 && x12 == x1 + x2
      if x2 == 0 mode := "Fill2" end; 
      x1'  = 2; 
      x2'  = -3;
      x12' = -1;
    case "Fill2" require x1 >= 0 && x2 >= 0 && x12 >= 0 && x12 == x1 + x2
      if x1 == 0 mode := "Fill1" end; 
      x1'  = -2; 
      x2'  = 1;
      x12' = -1;
  end;
  simulator.startTime := 0;
  simulator.endTime := 2.5;
  simulator.minSolverStep := 2.5;
  simulator.minLocalizationStep := 0.001;
  simulator.minComputationImprovement := 0;
end