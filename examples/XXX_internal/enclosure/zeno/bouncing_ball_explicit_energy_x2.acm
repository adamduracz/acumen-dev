/* Bouncing_ball explicit energy 2
 * Authors: Michal Konecny and Jan Duracz
 * Implements a version of the EBB model from the paper.
 */
class Main(simulator)
  private 
    mode := "Fly"; 
    x2 := 5; x2' := 0; x2'' := 0;  
    r2 := [0.0..100.0]; r2' := 0;
  end
  switch mode
    case "Fly" 
    require x2 >= 0 && r2 == x2'*x2' + 20*x2 + [-0.5..0.5]
      if x2 == 0 && x2' <= 0
        x2' := -0.5*x2';
        r2  := [0.0..0.25]*r2;
        mode := "Fly";
      end;
      x2'' = -10;
      r2'  = 0;
  end;
  simulator.startTime := 0;
  simulator.endTime := 3.5;
  simulator.minSolverStep := 0.04;
  simulator.minLocalizationStep := 0.008;
  simulator.minComputationImprovement := 0.001; 
end
