// Bouncing ball in lift program
//
// Authors: Eugenio Moggi and Adam Duracz

class Main(simulator)
  private l := create Lift(0,2) end
  simulator.endTime := 5;
end

class Lift(w,a)           // Speed,Acceleration of lift
  private
    h  := 0;   h' :=   w; // Position of lift
    w' := a;              // Acceleration of lift
    x  := 5;   x' :=   0; // Position of ball
    v  := 0;   v' := -10; // Speed of ball
    g  := 10;             // Gravity
    c  := 0.5;            // Coefficient of restitution
  end
  // Invariant
  claim x >= h;
  // Flow
  h' = w; 
  w' = a;
  x' = v;
  if x <= h && v <= w && a > -g
    v' = a
  else
    v' = -g
  end;
  // Jumps
  if x <= h && v < w
    v := w - c * (v - w)
  end 
end
