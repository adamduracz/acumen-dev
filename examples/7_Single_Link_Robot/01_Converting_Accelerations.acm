class Main(simulator)
  private 
    theta  := 0; theta' := 0;
    l  := 1; l' := 0;
    
    x_r := 1; y_r := 0;// This is the target
 
    x   := 0; x'  := 0; x'' := 0;
    y   := 0; y'  := 0; y'' := 0;

    k_p := 5; k_d := 1;

    x_b  := 1; x_b' := 0; x_b'' := 0; y_b  := 0; y_b' := 0; y_b'':=0;
    x_g  := 0; y_g  := 0; 
  
    theta_g := 0; theta_g' := 0; theta_g'' := 0;
    l_g := 1;  l_g' := 0; l_g'' := 0;  
    _3D := [];
end  
    theta' = 1; l' = 0.5*sin(theta);

    x_r = l * cos(theta); y_r = l * sin(theta);
   
    theta_g'' = k_p * (theta - theta_g) - k_d * theta_g';
    l_g''     = k_p * (l - l_g) - k_d * l_g';

    x_g = l_g * cos(theta_g);
    y_g = l_g * sin(theta_g);

   // For scribe notes:
   // x_b' = -y_b * theta_g' + l_g' * cos(theta_g);
   // y_b' =  x_b * theta_g' + l_g' * sin(theta_g);
    x_b'' = - y_b * theta_g'' - y_b' * theta_g' 
            - l_g'  * sin(theta_g) * theta_g' 
            + l_g'' * cos(theta_g);
             
    y_b'' =   x_b * theta_g'' + x_b' * theta_g'
            + l_g'  * cos(theta_g) * theta_g' 
            + l_g'' * sin(theta_g); 
    _3D = [
      ["Sphere", [x_r, 0, y_r], 0.1, [1, 0, 0], [0,0,0]],
      ["Sphere", [x_g, 0, y_g], 0.1, [0, 1, 0], [0,0,0]],
      ["Sphere", [x_b, 0, y_b], 0.1, [0, 0, 1], [0,0,0]]
    ];

end