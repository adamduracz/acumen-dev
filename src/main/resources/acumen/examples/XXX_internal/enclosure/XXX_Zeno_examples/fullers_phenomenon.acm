/* Fuller's Example
 * Author: Adam Duracz 
 * Implements Example 3 from the paper
 * "Zeno hybrid systems" by Zhang et al.
 */
class Main(simulator) 
 private 
   mode := "q1"; 
   x1 := -1; x1' := 1;  
   x2 := 1;  x2' := 1;
   C  := [0.001 .. 0.5] //FIXME This should start at 0
 end
 switch mode
   case "q1" assume x1 + C * x2 * x2 <= 0
     if x1 + C * x2 * x2 > 0 mode := "q2" end;
     x1'  = x2; 
     x2'  = 1;
   case "q2" assume x1 - C * x2 * x2 >= 0
     if x1 - C * x2 * x2 < 0 mode := "q1" end;
     x1'  = x2; 
     x2'  = -1;
 end
 simulator.precision := 10;
 simulator.startTime := 0;
 simulator.endTime := 3.5;
 simulator.initialConditionPadding := 0;
 simulator.extraPicardIterations := 20;
 simulator.maxPicardIterations := 200;
 simulator.maxEventSequenceLength := 30;
 simulator.minTimeStep := 0.001;
 simulator.maxTimeStep := 0.1;
 simulator.minImprovement := 0.1;
end
