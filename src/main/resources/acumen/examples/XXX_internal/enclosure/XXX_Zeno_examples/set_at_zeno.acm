/* This is the Set at Zeno model.
 * Author: Adam Duracz
 * The model is based on Example 1 on page 7
 * of paper "Dynamical Systems Revisited:
 * Hybrid Systems with Zeno Executions" by
 * Zhang, Johansson, Lygeros and Sastry.
 */
class Main(simulator)
 private
     mode := "q1";
     x1 := 2;   x1' := 0;
     x2 := 0;   x2' := 0;
     c  := 0.5;
 end
 switch mode
    case "q1" assume x1 >= 0
     if -0.1 <= x1 && x1 <= 0
     &&               x2 <= 0
       x1   := 5;
       x2   := -c * x2;
         mode := "q2"
     end;
     x1' = x2;
     x2' = -10;
    case "q2" assume x1 >= 3
     if 4.9 <= x1 && x1 <= 5
     &&              x2 <= 0
       x1   := 3;
       x2   := -c * x2;
         mode := "q2";
     end;
     if 2.9 <= x1 && x1 <= 3
     &&              x2 <= 0
       x1   := 0;
       x2   := -c * x2;
       mode := "q1";
     end;
     x1' = x2;
     x2' = -10;       
  end;
   simulator.precision := 10;
   simulator.startTime := 0;
   simulator.endTime := 13;
   simulator.initialConditionPadding := 0;
   simulator.extraPicardIterations := 20;
   simulator.maxPicardIterations := 200;
   simulator.maxEventSequenceLength := 30;
   simulator.minTimeStep := 0.0001;
   simulator.maxTimeStep := 1;
   simulator.minImprovement := 0.0001;
end