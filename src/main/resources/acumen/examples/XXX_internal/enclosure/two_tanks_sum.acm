////////////////////////////////////////////////
// This file is called two_tanks_sum.acm      //
// It implements the S2T model from the paper.//
////////////////////////////////////////////////
class Main(simulator) 
  private 
    mode := "Fill1"; 
    x1 := 1; x1' := 2;  
    x2 := 1; x2' := -3;
    x12 := 2; x12' := -1;
  end
  simulator.precision := 10;
  simulator.startTime := 0;
  simulator.endTime := 2.5;
  simulator.initialConditionPadding := 0;
  simulator.extraPicardIterations := 20;
  simulator.maxPicardIterations := 200;
  simulator.maxEventSequenceLength := 30;
  simulator.minTimeStep := 0.001;
  simulator.maxTimeStep := 0.1;
  simulator.minImprovement := 0.1;
  switch mode
    case "Fill1" assume x1 >= 0 && x2 >= 0 && x12 >= 0 && x12 == x1 + x2
      if x2 == 0 mode := "Fill2" end; 
      x1'  = 2; 
      x2'  = -3;
      x12' = -1;
    case "Fill2" assume x1 >= 0 && x2 >= 0 && x12 >= 0 && x12 == x1 + x2
      if x1 == 0 mode := "Fill1" end; 
      x1'  = -2; 
      x2'  = 1;
      x12' = -1;
  end
end